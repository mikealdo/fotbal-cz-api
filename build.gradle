apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: "com.gorylenko.gradle-git-properties"
apply plugin: 'com.jfrog.bintray'

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://repo.spring.io/milestone' }
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.3.6.RELEASE'
        classpath 'com.ofg:uptodate-gradle-plugin:+'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath "io.codearte.accurest:accurest-gradle-plugin:$accurestVersion"
        classpath 'com.blogspot.toomuchcoding:gradle-test-profiler:0.3.1'
        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.16'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'

        if (project.hasProperty("coverage")) {
            classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:1.0.2'
            classpath "org.sonarqube.gradle:gradle-sonarqube-plugin:1.0"
        }
    }
}

ext {
    buildNrLoc = project.hasProperty('buildNr') ? "${buildNr}" : "CD-" + getDate()
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

group = mavenProjectGroupId
version = project.hasProperty('currentVersion') ? project.getProperty('currentVersion') : buildNrLoc

sourceSets.main.java.srcDirs = []
sourceSets.main.groovy.srcDirs += ["src/main/java"]

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url 'http://dl.bintray.com/mdavidek1/rango'
    }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.springframework:spring-context-support"
    compile "org.codehaus.groovy:groovy-all"
    compile 'com.fasterxml.jackson.core:jackson-databind'
    compile 'com.google.guava:guava:19.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.tomcat.embed:tomcat-embed-el'
    compile 'com.jayway.jsonpath:json-path-assert:2.2.0'
    compile 'org.yaml:snakeyaml'
    compile 'org.hibernate:hibernate-validator'
    compile "org.aspectj:aspectjrt"
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile 'net.sf.ehcache:ehcache'
    compile "com.ofg:micro-infra-spring-boot-starter:$microInfraSpringVersion"
    compile "com.ofg:activity-tracker:$microInfraSpringVersion"
    compile "com.ofg:activity-tracker-boot-starter:$microInfraSpringVersion"
    compile 'org.jsoup:jsoup:1.8.3'
    compile "cz.mikealdo:football-domain:1.2"

    runtime 'cglib:cglib-nodep:3.2.3'
    runtime 'org.objenesis:objenesis:2.4'
    runtime 'org.aspectj:aspectjweaver'
    runtime 'com.h2database:h2'

    testCompile 'com.jayway.awaitility:awaitility:1.7.0'
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testCompile "com.ofg:micro-infra-spring-test:$microInfraSpringVersion"

    testCompile "org.gebish:geb-core:$gebVersion"
    testCompile "org.gebish:geb-spock:$gebVersion"
    testCompile "org.spockframework:spock-spring"
    testCompile "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    testCompile "org.seleniumhq.selenium:selenium-htmlunit-driver:2.52.0"
    testCompile "com.jayway.restassured:rest-assured:$restAssuredVersion"
    testCompile "com.jayway.restassured:spring-mock-mvc:$restAssuredVersion"
    testCompile "io.codearte.accurest:accurest-core:$accurestVersion"
    testCompile "cz.mikealdo:football-domain:1.1"
    testCompile 'junit:junit:4.12'

    // for easier contract devlopment - ide auto-completion support
    testCompileOnly "io.codearte.accurest:accurest-core:$accurestVersion"
}

task bootRunDev(type: org.springframework.boot.gradle.run.BootRunTask) {
	group = 'Application'
	doFirst() {
		main = project.mainClassName
		classpath = sourceSets.main.runtimeClasspath
		systemProperty 'APP_ENV', 'dev'
		systemProperty 'CONFIG_FOLDER', 'properties'
		systemProperty 'spring.profiles.active', 'dev,springCloud'
	}
}

task addHashFile << {
    ext.repo = org.ajoberstar.grgit.Grgit.open(project.file('.'))
    def headCommit = repo.head()
    ext.gitBranch = repo.branch.current.name
    ext.gitCommitId = headCommit.abbreviatedId
    ext.gitCommitTime = new Date(headCommit.time * 1000L).format("yyyy-MM-dd'T'HH:mm:ssZ")

    File resourcesMainDir = new File(project.buildDir, 'resources/main')
    resourcesMainDir.mkdirs()
    File gitPropertiesFile = new File(resourcesMainDir, 'git.properties')
    gitPropertiesFile.text = """git.branch=${gitBranch}
git.commit.id=${gitCommitId}
git.commit.time=${gitCommitTime}
"""
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'cz.mikealdo'
            artifactId 'fotbal-cz-api'
            version project.version
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'rango'
        name = 'fotbal-cz-api'
        userOrg = 'mdavidek1'
        desc = 'Fotbal CZ api stubs and microservice'
        publish = true
        issueTrackerUrl = 'https://github.com/mikealdo/fotbal-cz-api/issues'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mikealdo/fotbal-cz-api'
        labels = ['continous-delivery', 'rango']
        publicDownloadNumbers = true
        configurations = ['archives']
        version {
            name = project.version
            desc = 'Fotbal CZ Api'
            released  = new Date()
            vcsTag = project.version
        }
    }
}

task replaceInBintrayDescriptor() {
    replaceToken("\\{version\\}", project.version)
}

def replaceToken(fromString, toString) {
    def descriptorFileName = "deployBintray.json"
    def updatedContent = new File(descriptorFileName).getText('UTF-8').replaceAll(fromString, toString)
    new File(descriptorFileName).write(updatedContent, 'UTF-8')
}

apply from: 'gradle/cli.gradle'
apply from: 'gradle/test.gradle'
apply from: 'gradle/misc.gradle'
apply from: 'gradle/release.gradle'